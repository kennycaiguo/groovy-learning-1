/**
 * æ–¹æ³•å§”æ‰˜ï¼Œä¸€ä¸ª Manager å°†å·¥ä½œå§”æ‰˜ç»™ä¸€ä¸ª Worker å’Œä¸€ä¸ª Expertï¼Œä½¿ç”¨ MethodMissing() å’Œ ExpandoMetaClass å¯ä»¥å®ç°ã€‚
 * å¦‚æœåœ¨ Manager ä¸Šè°ƒç”¨çš„æ–¹æ³•ä¸å­˜åœ¨ï¼Œè¯¥å®ä¾‹çš„ methodMissing() ä¼šå°†è°ƒç”¨è·¯ç”±åˆ° Worker æˆ– Expertã€‚
 *
 * @author Harry Zhang 
 * @since 2020/1/21 10:52 AM
 */
class Worker1 {
    def simpleWork1(spec) { println "worker dose work1 with spec $spec" }

    def simpleWork2() { println "worker dose work2" }
}

class Expert1 {
    def advancedWork1(spec) { println "Expert dose work1 with spec $spec" }

    def advancedWork2(scope, spec) {
        println "Expert dose work2 with scope $scope spec $spec"
    }
}

class Manager1 {
    def worker = new Worker1()
    def expert = new Expert1()

    def schedule() { println "Scheduling..." }

    def methodMissing(String name, args) {
        println ">> interceping call to $name..."
        def delegateTo = null

        if (name.startsWith('simple')) delegateTo = worker
        if (name.startsWith('advanced')) delegateTo = expert

        if (delegateTo?.metaClass?.respondsTo(delegateTo, name, args)) {
            // this æ˜¯å½“å‰è¿è¡Œçš„ Manager1 å¯¹è±¡å®ä¾‹ï¼Œå³ï¼špeter
            Manager1 instance = this
            // ç»™ instance å®ä¾‹æ·»åŠ åŠ¨æ€æ–¹æ³•ï¼Œæ‰§è¡Œè€…æ˜¯å§”æ‰˜å¯¹è±¡ã€‚
            instance.metaClass."${name}" = { Object[] varArgs ->
                /*
                 * instance å®ä¾‹å°†æ–¹æ³•å§”æ‰˜ç»™å§”æ‰˜å®ä¾‹ã€‚
                 *
                 * æ–¹å¼1ï¼šä½¿ç”¨å§”æ‰˜å¯¹è±¡ç›´æ¥æ‰§è¡Œ invokeMethod() æ–¹æ³•ï¼ˆæ³¨æ„ä¸è¦å†™é”™äº†æ–¹æ³•åğŸ˜“ï¼Œç”±äºæŠŠ invokeMethod å†™æˆ invokMethod è¢«å‘äº†å¥½ä¹…ã€‚ã€‚ã€‚ï¼‰
                 *       delegateTo.invokeMethod(name, varArgs)
                 *
                 * æ–¹å¼2ï¼šä½¿ç”¨å§”æ‰˜å¯¹è±¡çš„metaClassè·å–åˆ°æ–¹æ³•å†æ‰§è¡Œ invoke()
                 */
                def method = delegateTo.metaClass.getMetaMethod(name, varArgs)
                method.invoke(delegateTo, varArgs)
            }
            // è°ƒç”¨å§”æ‰˜å®ä¾‹çš„æ–¹æ³•
            delegateTo.invokeMethod(name, args)
        } else {
            throw new MissingMethodException(name, Manager.class, args)
        }
    }
}

def peter = new Manager1()
peter.schedule()
peter.simpleWork1('fast')
peter.simpleWork1('quality')

peter.simpleWork2()
peter.simpleWork2()

peter.advancedWork1('fast')
peter.advancedWork1('quality')

peter.advancedWork2('prototype', 'fast')
peter.advancedWork2('product', 'quality')

try {
    peter.simpleWork3()
} catch (e) {
    println e
}
