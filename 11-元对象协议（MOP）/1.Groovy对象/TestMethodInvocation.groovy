/**
 * éªŒè¯ Groovy å¯¹ POGO å¯¹è±¡çš„æ–¹æ³•è°ƒç”¨æœºåˆ¶
 *
 * @author Harry Zhang 
 * @since 2020/1/16 2:00 PM
 */
class TestMethodInvocation extends GroovyTestCase {

    /**
     * 1. Integer æ²¡æœ‰å®ç° GroovyInterceptable
     * 2. ç›®æ ‡æ–¹æ³•å­˜åœ¨ metaClass ä¸­ï¼Œ ğŸ‘‰ è°ƒç”¨ metaClass ä¸­çš„ toString
     */
    void testInterceptedMethodCallOnPojo() {
        def val = new Integer(3)
        Integer.metaClass.toString = { -> 'intercepted' }
        assertEquals("intercepted", val.toString())
    }

    /**
     * 1. AnInterceptable å®ç°äº† GroovyInterceptableï¼Œä¸è®ºç›®æ ‡æ–¹æ³•æ˜¯å¦å­˜åœ¨éƒ½ä¼šè°ƒç”¨ ğŸ‘‰ invokeMethod()
     */
    void testInterceptableCalled() {
        def obj = new AnInterceptable()
        // invokeMethod() è¿”å›äº† interceptedï¼Œæ‰€ä»¥ä¸‹é¢ä¸¤ä¸ªæµ‹è¯•éƒ½èƒ½é€šè¿‡ã€‚
        assertEquals('intercepted', obj.existingMethod())
        assertEquals('intercepted', obj.nonExistingMethod())
    }

    /**
     * 1. AGroovyObject æ²¡å®ç° GroovyInterceptable;
     * 2. ç›®æ ‡æ–¹æ³• existingMethod2 å­˜åœ¨ metaClass ä¸­ ğŸ‘‰ è°ƒç”¨ metaClass ä¸­çš„ existingMethod2
     */
    void testInterceptedExistingMethodCalled() {
        // ä¿®æ”¹æ–¹æ³•ï¼Œè¿”å› 'intercepted'ï¼ˆåŸæœ¬è¿”å› 'existingMethod2' ï¼‰
        AGroovyObject.metaClass.existingMethod2 = { -> 'intercepted' }
        def obj = new AGroovyObject()
        assertEquals('intercepted', obj.existingMethod2())
    }

    /**
     * 1. AGroovyObject æ²¡å®ç° GroovyInterceptable;
     * 2. ç›®æ ‡æ–¹æ³•ä¸å­˜åœ¨ metaClass ä¸­ ğŸ‘‰ è°ƒç”¨ POGO è‡ªèº«çš„ existingMethod
     */
    void testUnInterceptedExistingMethodCalled() {
        def obj = new AGroovyObject()
        assertEquals('existingMethod', obj.existingMethod())
    }

    /**
     * 1. AGroovyObject æ²¡å®ç° GroovyInterceptable;
     * 2. ç›®æ ‡æ–¹æ³•æ²¡åœ¨ metaClass ä¸­ï¼›
     * 3. POGO è‡ªèº«ä¹Ÿæ²¡æœ‰ç›®æ ‡æ–¹æ³•ï¼›
     * 4. å­˜åœ¨ä»¥ç›®æ ‡æ–¹æ³•åçš„å±æ€§ï¼Œå¹¶ä¸”å±æ€§æ˜¯é—­åŒ…ç±»å‹ ğŸ‘‰ è°ƒç”¨é—­åŒ…
     */
    void testPropertyThatIsCLousreCalled() {
        def obj = new AGroovyObject()
        assertEquals('closure called', obj.closureProp())
    }

    /**
     * 1. ClassWithInvokeAndMissingMethod æ²¡å®ç° GroovyInterceptable;
     * 2. ç›®æ ‡æ–¹æ³• existingMethod() æ²¡åœ¨ metaClass ä¸­ ğŸ‘‰ è°ƒç”¨è‡ªèº«çš„ existingMethodï¼›
     * -----------------------------------------------------------------------
     * 2. ç›®æ ‡æ–¹æ³• nonExistingMethod() æ²¡åœ¨ metaClass ä¸­ï¼›
     * 3. ä¸å­˜åœ¨ä»¥ nonExistingMethod() å‘½åçš„å±æ€§ï¼›
     * 4. å­˜åœ¨ methodMissing() ğŸ‘‰ è°ƒç”¨ methodMissingã€‚
     */
    void testMethodMissingCalledOnlyForNonExisting() {
        def obj = new ClassWithInvokeAndMissingMethod()
        assertEquals('existingMethod', obj.existingMethod())
        assertEquals('missing called', obj.nonExistingMethod())
    }

    /**
     * 1. ClassWithInvokeOnly  æ²¡å®ç° GroovyInterceptable;
     * 2. ç›®æ ‡æ–¹æ³• existingMethod æ²¡åœ¨ metaClass ä¸­ï¼Œè°ƒç”¨è‡ªèº«çš„ existingMethodã€‚
     * --------------------------------------------------------------------
     * 2. ç›®æ ‡æ–¹æ³• nonExistingMethod æ²¡åœ¨ metaClass ä¸­ï¼›
     * 3. ç›®æ ‡æ–¹æ³• nonExistingMethod ä¹Ÿæ²¡åœ¨ POGO ä¸­ï¼›
     * 4. ä¸å­˜åœ¨ä»¥ nonExistingMethod å‘½åçš„å±æ€§ï¼›
     * 5. ä¸å­˜åœ¨ methodMissingï¼Œè°ƒç”¨ invokeMethod()
     */
    void testInvokeMethodCalledForOnlyNonExisting() {
        def obj = new ClassWithInvokeOnly()
        assertEquals('existingMethod', obj.existingMethod())
        assertEquals('invoke called', obj.nonExistingMethod())
    }

    /**
     * 1. TestMethodInvocation æ²¡å®ç° GroovyInterceptableï¼›
     * 2. nonExistingMethod() æ²¡åœ¨ metaClass ä¸­ï¼›
     * 3. nonExistingMethod() æ²¡åœ¨ POGO ä¸­ï¼›
     * 4. æ²¡æœ‰ nonExistingMethod è¿™ä¸ªå±æ€§ï¼›
     * 5. æ²¡æœ‰ methodMissing()ï¼›
     * 6. æ²¡æœ‰ invokeMethod() ğŸ‘‰ æŠ›å‡º MissingMethodException
     */
    void testMethodFailsOnNonExistent() {
        def obj = new TestMethodInvocation()
        shouldFail(MissingMethodException) { obj.nonExistingMethod() }
    }
}


class AnInterceptable implements GroovyInterceptable {
    def existingMethod() {}

    def invokeMethod(String name, args) { 'intercepted' }
}

class AGroovyObject {
    def existingMethod() { 'existingMethod' }

    def existingMethod2() { 'existingMethod2' }
    def closureProp = { 'closure called' }
}

class ClassWithInvokeAndMissingMethod {
    def existingMethod() { 'existingMethod' }

    def invokeMethod(String name, args) { 'invoke called' }

    def methodMissing(String name, args) { 'missing called' }
}

class ClassWithInvokeOnly {
    def existingMethod() { 'existingMethod' }

    def invokeMethod(String name, args) { 'invoke called' }
}
